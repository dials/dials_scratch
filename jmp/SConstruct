
def tool_distutils(env):
    """Add Python Distutils variables to the scons environment"""
    import distutils.sysconfig, os
    
    cc, basecflags, opt, so_ext = distutils.sysconfig.get_config_vars(
            'CC', 'BASECFLAGS', 'OPT', 'SO')

    env.AppendUnique(CPPPATH=[distutils.sysconfig.get_python_inc()])
    env.AppendUnique(CPPFLAGS=basecflags)
    env.AppendUnique(LIBPATH=[distutils.sysconfig.PREFIX+"/libs"])
    env['SHLIBPREFIX'] = ""
    env['SHLIBSUFFIX'] = so_ext

    if cc:
        env['CC'] = cc

    #if opt:
    #    env.AppendUnique(CPPFLAGS=opt)

def tool_boost(env):
    """Add BOOST variables to the scons environment"""
    env.AppendUnique(LIBS='boost_python')

def tool_cctbx(env):
    """Add CCTBX variables to the scons environment"""
    import os
    
    try:
        build = os.environ['LIBTBX_BUILD']
        source = os.environ['LIBTBX_SOURCE']
    except:
        print ('Can\'t find CCTBX source and build directories\n'
               'Try setting the following environment variables\n'
               ' - LIBTBX_BUILD\n'
               ' - LIBTBX_SOURCE\n')
        raise

    env.AppendUnique(CPPPATH=[build + "/include", source + "/cctbx_project"])


AddOption('--build-base',
          dest='build_base',
          type='string',
          nargs=1,
          action='store',
          metavar='DIR',
          help='build directory',
          default='build')

AddOption('--prefix',
          dest='prefix',
          type='string',
          nargs=1,
          action='store',
          metavar='DIR',
          help='installation prefix',
          default='install')

build_dir = GetOption('build_base') + '/dials'
install_dir = GetOption('prefix')

env = Environment(tools=['default', tool_distutils, tool_boost, tool_cctbx])

env.SConscript('dials/SConscript', variant_dir=build_dir, exports={'env' : env })



